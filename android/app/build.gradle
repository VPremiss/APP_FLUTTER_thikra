plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def keystorePropertiesFile = rootProject.file("app/signing.properties")
def keystoreProperties = new Properties()

if (keystorePropertiesFile.exists()) {
    println "INFO: Found signing.properties file. Using its credentials..."
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    // println "INFO: Loaded keystore properties: ${keystoreProperties}"
} else {
    println "WARNING: Missing signing.properties file. Release build won't be signed."
}

android {
    namespace = "dev.vpremiss.thikra_app"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId = "dev.vpremiss.thikra_app"
        minSdk = 23
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    buildTypes {
        release {
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.create("release") {
                    storeFile = file(keystoreProperties['storeFile'])
                    storePassword = keystoreProperties['storePassword']
                    keyAlias = keystoreProperties['keyAlias']
                    keyPassword = keystoreProperties['keyPassword']
                }
            } else {
                println "INFO: Using debug signing configuration for release build."
                signingConfig = signingConfigs.debug
            }
        }
    }
}

flutter {
    source = "../.."
}
